//Experimental query to detect repeated MFA failures over a period of time. Should be tested and tuned before use.

//Threshold for number of failures in a given time window. 4 worked well based on tests
let failureCountThreshold = 4;
//Time range to look back for failures
let timeRange = ago(7d);
//Time window for failures to occur in. 20m worked well based on tests
let authenticationWindow = 20m;

AADSignInEventsBeta 
| project Timestamp,AuthenticationRequirement,ErrorCode,RequestId,AccountObjectId
| where Timestamp >= timeRange
| where AuthenticationRequirement == "multiFactorAuthentication"
//Based on analyzing sign-in logs 500121 is consistent with MFA not being completed.
| where ErrorCode == "500121"
//Multiple failures can be generated by the same Request Id across a varying timeframe. Group based on reqeust Id and timestamp for better accuracy. 60s worked well based on tests
| summarize by RequestId,bin(Timestamp, 60s),AccountObjectId
| extend FailureOrSuccess = "Failure"
| summarize FailureCount = countif(FailureOrSuccess=="Failure")
by bin(Timestamp, authenticationWindow),AccountObjectId
| where FailureCount>=failureCountThreshold